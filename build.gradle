plugins {
    id 'java'
    id 'com.google.protobuf' version '0.9.4'
    id "io.gatling.gradle" version "3.9.2.1"
    id "com.google.cloud.tools.jib" version "3.3.2"
    id 'application'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

application {
    mainClass = 'org.pidu.javamalloc.Main'
}

group = 'org.pidu.javamalloc'
version = '1.0-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    runtimeOnly 'io.grpc:grpc-netty-shaded:1.56.0'
    implementation 'io.grpc:grpc-protobuf:1.56.0'
    implementation 'io.grpc:grpc-stub:1.56.0'
    implementation 'com.google.protobuf:protobuf-java:3.23.4'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'

    gatling "com.github.phisgr:gatling-grpc:0.16.0"
    gatling "com.github.phisgr:gatling-grpc-kt:0.15.1"
    gatling 'io.gatling:gatling-app:3.9.2'
    gatling 'io.gatling.highcharts:gatling-charts-highcharts:3.9.2'
    gatling 'com.google.protobuf:protobuf-java:3.23.4'

    compileOnly 'org.apache.tomcat:annotations-api:6.0.53' // necessary for Java 9+
}

jar {
    manifest {
        attributes 'Main-Class': 'org.pidu.javamalloc.Main'
    }
}

test {
    useJUnitPlatform()
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.23.4"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.56.0'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

jib {
    from {
        image = "eclipse-temurin:17-jdk-jammy"
    }
    to {
        image = "pidu/javamalloc"
    }
    container {
        jvmFlags = [
                "-Xms128m", "-Xmx150m",
                "-Dcom.sun.management.jmxremote",
                "-Dcom.sun.management.jmxremote.port=5015",
                "-Dcom.sun.management.jmxremote.rmi.port=5015",
                "-Dcom.sun.management.jmxremote.ssl=false",
                "-Dcom.sun.management.jmxremote.authenticate=false",
                "-Dcom.sun.management.jmxremote.local.only=false",
                "-Djava.rmi.server.hostname=localhost"
        ]
        mainClass = "org.pidu.javamalloc.Main"
    }
}

gatling {
    jvmArgs = [
            "-Xmx4G",
            "-XX:+HeapDumpOnOutOfMemoryError"
    ]
}
